apply plugin: 'com.android.application'

// Local keystore for production signing
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.igrow.android"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        // need multiDex enables as we pop the 0xffff number of
        // methods limit
        multiDexEnabled  true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

repositories {
    jcenter()
    mavenLocal()  // Add Maven local repository for igrow-api-java
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // As we allow running on API level >=19 , need to patch the
    // classloader on the target, which this library does for us.
    implementation 'com.android.support:multidex:1.0.2'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-android
    implementation group: 'org.slf4j', name: 'slf4j-android', version: '1.7.22'
    // Our local API dependencies
    implementation 'com.igrow:igrow-api-java:1.0-SNAPSHOT'

    // Google Sign-in
    implementation 'com.google.android.gms:play-services-auth:9.8.0'

    // Google Dagger
    implementation 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.0.1'

    //implementation 'org.greenrobot:eventbus:3.0.0'

    // Room
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0'

    // Lifecycle extensions
    implementation 'android.arch.lifecycle:extensions:1.0.0'

    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support:support-v4:27.0.0'
    implementation 'com.android.support:recyclerview-v7:27.0.0'
    implementation 'com.android.support:cardview-v7:27.0.0'
    implementation 'com.android.support:design:27.0.0'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1'

    // Mapbox
    // TODO: add support for MapBox in a separate branch
    implementation ('com.mapbox.mapboxsdk:mapbox-android-sdk:4.1.1@aar'){
        transitive=true
    }
    // osmdroid
    implementation 'org.osmdroid:osmdroid-android:5.5:release@aar'

    // Testing
    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.12'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:1.10.19'

    androidTestImplementation 'com.android.support:appcompat-v7:27.0.0'
    androidTestImplementation 'com.android.support:support-v4:27.0.0'
    androidTestImplementation 'com.android.support:recyclerview-v7:27.0.0'
    androidTestImplementation 'com.android.support:cardview-v7:27.0.0'
    androidTestImplementation 'com.android.support:design:27.0.0'
    // Android JUnit Runner
    androidTestImplementation 'com.android.support.test:runner:1.0.1'

    // JUnit4 Rules
    androidTestImplementation 'com.android.support.test:rules:1.0.1'

    // Espresso core
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions,
    //                      Accessibility checks, CountingIdlingResource
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.1'
    // Espresso-web for WebView support
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.1'
    // Espresso-idling-resource for synchronization with background jobs
    androidTestImplementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.1'
    // Espresso-intents for validation and stubbing of Intents
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.1'
    // UiAutomator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'

    testImplementation 'org.robolectric:robolectric:3.5.1'
}

apply plugin: 'com.google.gms.google-services'
