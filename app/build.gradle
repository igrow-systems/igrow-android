apply plugin: 'com.android.application'

// Local keystore for production signing
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.igrow.android"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        // need multiDex enables as we pop the 0xffff number of
        // methods limit
        multiDexEnabled  true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    jcenter()
    mavenLocal()  // Add Maven local repository for igrow-api-java
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // As we allow running on API level >=19 , need to patch the
    // classloader on the target, which this library does for us.
    compile 'com.android.support:multidex:1.0.0'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-android
    compile group: 'org.slf4j', name: 'slf4j-android', version: '1.7.22'
    // Our local API dependencies
    compile 'com.igrow:igrow-api-java:1.0-SNAPSHOT'

    // Google Sign-in
    compile 'com.google.android.gms:play-services-auth:9.8.0'

    // Google Dagger
    compile 'com.google.dagger:dagger:2.8'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'

    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'

    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'

    // Mapbox
    // TODO: add support for MapBox in a separate branch
    compile ('com.mapbox.mapboxsdk:mapbox-android-sdk:4.1.1@aar'){
        transitive=true
    }
    // osmdroid
    compile 'org.osmdroid:osmdroid-android:5.5:release@aar'

    // Testing
    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'

    androidTestCompile 'com.android.support:appcompat-v7:25.1.1'
    androidTestCompile 'com.android.support:support-v4:25.1.1'
    androidTestCompile 'com.android.support:recyclerview-v7:25.1.1'
    androidTestCompile 'com.android.support:cardview-v7:25.1.1'
    androidTestCompile 'com.android.support:design:25.1.1'
    // Android JUnit Runner
    androidTestCompile 'com.android.support.test:runner:0.5'

    // JUnit4 Rules
    androidTestCompile 'com.android.support.test:rules:0.5'

    // Espresso core
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions,
    //                      Accessibility checks, CountingIdlingResource
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    // Espresso-web for WebView support
    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.2'
    // Espresso-idling-resource for synchronization with background jobs
    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'
    // Espresso-intents for validation and stubbing of Intents
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'

    // UiAutomator
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}

apply plugin: 'com.google.gms.google-services'
